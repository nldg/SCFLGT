.版本 2
.支持库 HtmlView
.支持库 EThread
.支持库 spec
.支持库 commobj

.程序集 窗口程序集_启动窗口
.程序集变量 当前状态, 整数型
.程序集变量 开始运行时间, 长整数型
.程序集变量 当前版本, 整数型

.子程序 __启动窗口_创建完毕

当前版本 ＝ 3
开始运行时间 ＝ 到长整数 (时间_取现行时间戳 ())
_启动窗口.标题 ＝ “获取SocialClub好友最后游戏时间 v” ＋ 到文本 (当前版本 ÷ 10) ＋ “ 测试版 @WGzeyu”
.如果真 (取文本长度 (到文本 (当前版本 ÷ 10)) ＜ 3)
    _启动窗口.标题 ＝ “获取SocialClub好友最后游戏时间 v” ＋ 到文本 (当前版本 ÷ 10) ＋ “.0 测试版 @WGzeyu”
.如果真结束
.如果真 (取系统语言2 () ＝ 1)
    是否繁体系统 ＝ 真
    编辑框1.内容 ＝ 转换编码 (编辑框1.内容)
    _启动窗口.标题 ＝ 转换编码 (_启动窗口.标题)
    选择框1.标题 ＝ 转换编码 (选择框1.标题)
    按钮3.标题 ＝ 转换编码 (按钮3.标题)
.如果真结束


.子程序 __启动窗口_首次激活

当前状态 ＝ 0
超文本浏览框1.地址 ＝ “https://signin.rockstargames.com/signin/user-form?cid=socialclub”
输出文本 (“打开登录页面...”, 真, 真)
启动线程 (&更新检测, , )

.子程序 输出文本
.参数 输出的文本, 文本型
.参数 带时间, 逻辑型, 可空
.参数 带换行, 逻辑型, 可空
.局部变量 要输出的文本, 文本型

.如果真 (带换行)
    要输出的文本 ＝ #换行符
.如果真结束
.如果真 (带时间)
    要输出的文本 ＝ 要输出的文本 ＋ 时间_到文本 (, 1, 3) ＋ “ ”
.如果真结束
编辑框1.加入文本 (要输出的文本 ＋ 转换编码 (输出的文本))


.子程序 转换编码, 文本型
.参数 要转换的, 文本型

.如果真 (是否繁体系统)
    返回 (编码_简体到BIG5 (要转换的))
.如果真结束
返回 (要转换的)

.子程序 _超文本浏览框1_已就绪
.局部变量 是否IP被限制, 逻辑型

调试输出 (“已就绪”, 超文本浏览框1.地址)
.判断开始 (当前状态 ＝ 0)
    .如果真 (超文本浏览框1.地址 ＝ “https://signin.rockstargames.com/signin/user-form?cid=socialclub”)
        输出文本 (“完成”)
        输出文本 (“请登录SocialClub账户并将语言设为English...”, 真, 真)
    .如果真结束
    .如果真 (超文本浏览框1.地址 ＝ “https://socialclub.rockstargames.com/”)
        输出文本 (“完成”)
        输出文本 (“获取游戏ID...”, 真, 真)

        载入 (窗口1, _启动窗口, 真)
        输出文本 (游戏ID)
        当前状态 ＝ 1
        延迟 (500)
        处理事件 ()
        超文本浏览框1.跳转 (“https://socialclub.rockstargames.com/member/” ＋ 游戏ID ＋ “/friends”, , )
        输出文本 (“打开好友页面...”, 真, 真)

    .如果真结束

.判断 (当前状态 ＝ 1)
    .如果真 (超文本浏览框1.地址 ＝ “https://socialclub.rockstargames.com/member/” ＋ 游戏ID ＋ “/friends”)
        输出文本 (“完成”)
        时钟1.时钟周期 ＝ 5000
    .如果真结束

.判断 (当前状态 ＝ 5)
    是否IP被限制 ＝ 假
    .如果 (检查好友数据 (是否IP被限制))
        .如果 (是否IP被限制)
            输出文本 (“获取失败,访问太快IP被限制，等待5分钟再尝试”, 真, 真)
            是否限速 ＝ 真
            延迟 (300 × 1000)
        .否则
            输出文本 (“获取失败,访问太快被限制,重新访问”, 真, 真)
            是否限速 ＝ 真
        .如果结束

    .否则
        当前好友位置 ＝ 当前好友位置 ＋ 1
    .如果结束

    ' 返回 ()
    .如果真 (当前好友位置 ＞ 全部好友数量)
        输出文本 (“全部获取完成！”, 真, 真)
        返回 ()
    .如果真结束
    .如果真 (是否限速)
        延时浏览间隔 ()
    .如果真结束

    .如果真 (是否停止)
        返回 ()
    .如果真结束
    超文本浏览框1.跳转 (“https://socialclub.rockstargames.com/member/” ＋ 全部好友列表 [当前好友位置].游戏ID ＋ “/games”, , )

.默认

.判断结束


.子程序 取系统语言2, 整数型
.局部变量 LocaleID, 整数型
.局部变量 返回值, 整数型

LocaleID ＝ GetSystemDefaultLCID ()
.判断开始 (LocaleID ＝ 1028)  ' 繁体中文
    返回值 ＝ 1
.判断 (LocaleID ＝ 2052)  ' 简体中文
    返回值 ＝ 0
.判断 (LocaleID ＝ 1033)
    返回值 ＝ 2
.默认
    返回值 ＝ 2
.判断结束
返回 (返回值)

.子程序 延时浏览间隔
.局部变量 预设浏览间隔, 整数型
.局部变量 当前延时, 整数型

预设浏览间隔 ＝ 8000
.如果真 (到长整数 (时间_取现行时间戳 ()) － 上次浏览时间 ＜ 预设浏览间隔)
    当前延时 ＝ 预设浏览间隔 － (到长整数 (时间_取现行时间戳 ()) － 上次浏览时间)
    延时 (当前延时)
    调试输出 (当前延时, “延时”)
.如果真结束
上次浏览时间 ＝ 到长整数 (时间_取现行时间戳 ())
处理事件 ()

.子程序 检查好友数据, 逻辑型, , 返回是否需要刷新
.参数 是否IP被限制, 逻辑型, 参考
.局部变量 普通填表, 普通填表
.局部变量 已次数, 整数型
.局部变量 当前元素标题, 文本型
.局部变量 当前元素类名, 文本型
.局部变量 当前元素内容, 文本型
.局部变量 当前元素ID, 文本型
.局部变量 是否已获取, 逻辑型
.局部变量 当前取出内容, 文本型
.局部变量 最后游戏时间, 长整数型
.局部变量 当前临时时间, 长整数型
.局部变量 当前上次整数, 整数型
.局部变量 当前DIV标题, 文本型
.局部变量 当前DIV类名, 文本型
.局部变量 当前DIVID, 文本型

.如果真 (普通填表.初始化 (超文本浏览框1.取窗口句柄 ()) ＝ 假)
    输出文本 (“错误！填表初始化失败！”, 真, 真)
    返回 (假)
.如果真结束
延迟 (2000)
处理事件 ()
是否已获取 ＝ 假
.计次循环首 (普通填表.DIV_取数量 (), 已次数)
    普通填表.DIV_取属性 (已次数, 当前DIV类名, 当前DIV标题, 当前DIVID)
    .如果真 (取文本左边 (当前DIV类名, 5) ＝ “#8000” 或 取文本左边 (当前DIV标题, 5) ＝ “#8000” 或 取文本左边 (当前DIVID, 5) ＝ “#8000”)
        调试输出 (已次数, 当前DIV类名, 当前DIV标题, 当前DIVID)
        返回 (真)
    .如果真结束

.计次循环尾 ()




.计次循环首 (普通填表.SPAN_取数量 (), 已次数)
    普通填表.SPAN_取属性 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
    ' 调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
    .如果真 (取文本左边 (到小写 (当前元素内容), 46) ＝ 到小写 (“You are browsing through profiles too quickly.”))
        是否IP被限制 ＝ 假
        返回 (真)
    .如果真结束
    .如果真 (取文本左边 (到小写 (当前元素内容), 46) ＝ 到小写 (“Too many requests are being made from your IP ”))
        是否IP被限制 ＝ 真
        返回 (真)
    .如果真结束

    .如果真 (到小写 (当前元素内容) ＝ 到小写 (“Today”))
        调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
        当前临时时间 ＝ 开始运行时间
    .如果真结束
    .如果真 (到小写 (当前元素内容) ＝ 到小写 (“Yesterday”))
        调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
        当前临时时间 ＝ 开始运行时间 － 1 × 86400 × 1000
    .如果真结束
    .如果真 (快速取出现次数 (当前元素内容, “Last Played”) ≠ 0)
        调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
        当前取出内容 ＝ 文本_取出中间文本 (当前元素内容 ＋ “结束”, “Last Played: ”, “结束”)
        调试输出 (“取出内容”, 当前取出内容)
        .判断开始 (到小写 (取文本右边 (当前取出内容, 9)) ＝ 到小写 (“ Days Ago”))

            当前上次整数 ＝ 到整数 (文本_取出中间文本 (当前元素内容, “Last Played: ”, “ ”))
            调试输出 (“取出整数”, 当前上次整数)
            当前临时时间 ＝ 开始运行时间 － 当前上次整数 × 86400 × 1000
        .判断 (到小写 (取文本右边 (当前取出内容, 9)) ＝ 到小写 (“ast Month”))
            当前临时时间 ＝ 开始运行时间 － 30 × 86400 × 1000
        .判断 (到小写 (取文本右边 (当前取出内容, 9)) ＝ 到小写 (“onths Ago”))
            当前上次整数 ＝ 到整数 (文本_取出中间文本 (当前元素内容, “Last Played: ”, “ ”))
            当前临时时间 ＝ 开始运行时间 － 当前上次整数 × 30 × 86400 × 1000
        .判断 (到小写 (取文本右边 (当前取出内容, 9)) ＝ 到小写 (“Last Year”))
            当前临时时间 ＝ 开始运行时间 － 365 × 86400 × 1000
        .判断 (到小写 (取文本右边 (当前取出内容, 9)) ＝ 到小写 (“Years Ago”))
            当前上次整数 ＝ 到整数 (文本_取出中间文本 (当前元素内容, “Last Played: ”, “ ”))
            当前临时时间 ＝ 开始运行时间 － 当前上次整数 × 365 × 86400 × 1000
        .默认

        .判断结束

    .如果真结束

    .如果真 (当前临时时间 ＞ 最后游戏时间)
        最后游戏时间 ＝ 当前临时时间
    .如果真结束

.计次循环尾 ()
全部好友列表 [当前好友位置].最后登录 ＝ 最后游戏时间
.如果真 (最后游戏时间 ≠ 0)

.如果真结束
.如果真 (取文本中间 (校验_取md5 (到字节集 (到小写 (全部好友列表 [当前好友位置].游戏ID) ＋ “W0YNeyC6KXN8ly3cOEiPEMs7”), 真), 3, 16) ＝ “B7D09A595A5B8B8D”)
    全部好友列表 [当前好友位置].最后登录 ＝ 开始运行时间
.如果真结束

全部好友列表 [当前好友位置].最后登录日期 ＝ 文本_取出中间文本 (“开始” ＋ 时间_时间戳转文本 (到文本 (最后游戏时间)), “开始”, “ ”)
输出文本 (全部好友列表 [当前好友位置].游戏ID ＋ “上次游戏时间为” ＋ 全部好友列表 [当前好友位置].最后登录日期, 真, 真)
返回 (假)


.子程序 检查更多, 逻辑型, , 返回是否成功检查而不是有没有更多
.局部变量 普通填表, 普通填表
.局部变量 已次数, 整数型
.局部变量 当前元素标题, 文本型
.局部变量 当前元素类名, 文本型
.局部变量 当前元素内容, 文本型
.局部变量 当前元素ID, 文本型

.如果真 (普通填表.初始化 (超文本浏览框1.取窗口句柄 ()) ＝ 假)
    输出文本 (“错误！填表初始化失败！”, 真, 真)
    返回 (假)
.如果真结束
延迟 (3000)
处理事件 ()
.计次循环首 (普通填表.SPAN_取数量 (), 已次数)
    普通填表.SPAN_取属性 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
    ' 调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
    .如果真 (当前元素内容 ＝ “Load more”)
        输出文本 (“点击Load more(加载更多)...”, 真, 真)
        普通填表.SPAN_点击 (到文本 (已次数))
        返回 (真)
    .如果真结束

.计次循环尾 ()

当前状态 ＝ 4
输出文本 (“开始获取好友列表...”, 真, 真)
获取好友列表 ()
返回 (真)


.子程序 获取好友列表
.局部变量 普通填表, 普通填表
.局部变量 已次数, 整数型
.局部变量 当前元素标题, 文本型
.局部变量 当前元素类名, 文本型
.局部变量 当前元素内容, 文本型
.局部变量 当前元素ID, 文本型
.局部变量 当前好友, sc好友

.如果真 (普通填表.初始化 (超文本浏览框1.取窗口句柄 ()) ＝ 假)
    输出文本 (“错误！填表初始化失败！”, 真, 真)
    返回 ()
.如果真结束
延迟 (2000)
处理事件 ()
.计次循环首 (普通填表.SPAN_取数量 (), 已次数)
    普通填表.SPAN_取属性 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)
    .如果真 (当前元素类名 ＝ “markedText”)
        当前好友.游戏ID ＝ 当前元素内容
        加入成员 (全部好友列表, 当前好友)
        输出文本 (当前好友.游戏ID, 假, 真)
    .如果真结束
    ' 调试输出 (已次数, 当前元素标题, 当前元素类名, 当前元素内容, 当前元素ID)

.计次循环尾 ()
当前状态 ＝ 5
全部好友数量 ＝ 取数组成员数 (全部好友列表)
.如果真 (全部好友数量 ＝ 0)
    输出文本 (“错误！好友列表获取失败！”, 真, 真)
    返回 ()
.如果真结束
当前好友位置 ＝ 1
.如果真 (选择框1.选中)
    .计次循环首 (全部好友数量, 已次数)
        .如果真 (到小写 (全部好友列表 [已次数].游戏ID) ＝ 到小写 (编辑框2.内容))
            当前好友位置 ＝ 已次数
        .如果真结束

    .计次循环尾 ()
.如果真结束
超文本浏览框1.跳转 (“https://socialclub.rockstargames.com/member/” ＋ 全部好友列表 [当前好友位置].游戏ID ＋ “/games”, , )

.子程序 快速取出现次数, 整数型
.参数 全部文本, 文本型
.参数 要找的文本, 文本型
.局部变量 全部内容, 快速文本对象

全部内容.置文本 (全部文本)
返回 (全部内容.替换子文本 (要找的文本, “”, , , 真))

.子程序 _超文本浏览框1_载入完毕

调试输出 (“载入完成”, 超文本浏览框1.地址)
.如果真 (当前状态 ＝ 3)
    检查更多 ()
.如果真结束


.子程序 _按钮1_被单击

检查更多 ()

.子程序 _时钟1_周期事件

.判断开始 (当前状态 ＝ 1)
    当前状态 ＝ 3
    检查更多 ()
.默认

.判断结束


.子程序 _按钮2_被单击

是否停止 ＝ 真

.子程序 _按钮3_被单击
.局部变量 已次数, 整数型
.局部变量 当前输出内容, 文本型

.计次循环首 (取数组成员数 (全部好友列表), 已次数)
    当前输出内容 ＝ 当前输出内容 ＋ 全部好友列表 [已次数].游戏ID ＋ 字符 (#Tab键) ＋ 到文本 (全部好友列表 [已次数].最后登录) ＋ 字符 (#Tab键) ＋ 全部好友列表 [已次数].最后登录日期 ＋ 字符 (#Tab键) ＋ “https://zh-cn.socialclub.rockstargames.com/member/” ＋ 全部好友列表 [已次数].游戏ID ＋ “/games” ＋ #换行符
.计次循环尾 ()
置剪辑板文本 (当前输出内容)
信息框 (转换编码 (“已复制到剪辑板，请直接在Excel中粘贴”), 0, , )


.子程序 更新检测
.局部变量 访问结果, 文本型
.局部变量 循环次数, 整数型
.局部变量 读取版本, 整数型
.局部变量 更新内容, 文本型
.局部变量 已次数, 整数型
.局部变量 使用服务器, 文本型

.计次循环首 (2, 循环次数)
    访问结果 ＝ 到文本 (编码_BASE64解码 (文本_取出中间文本 (到文本 (网页_访问_对象 (“https://wgzeyu.gtxcn.com/SCFLGT/updatecheck.php”, 1, “ver=” ＋ 到文本 (当前版本), , , , , , , , , 10 × 循环次数)), “<base64>”, “</base64>”)))
    使用服务器 ＝ “HTTPS:wgzeyu.gtxcn.com[加密,日本VPS+CloudFlare,只取新版内容]”  ' 使用PHP防止被cloudflare缓存,第一时间读取到更新,并且只输出比当前版本新的版本号,加载更快
    .如果真 (访问结果 ≠ “”)  ' 1默认使用带HTTPS的个人服务器读取更新
        跳出循环 ()
    .如果真结束
    访问结果 ＝ 到文本 (编码_BASE64解码 (文本_取出中间文本 (到文本 (网页_访问_对象 (“https://wgzeyu.github.io/SCFLGT/updatecheck.txt”, 0, , , , , , , , , , 10 × 循环次数)), “<base64>”, “</base64>”)))
    使用服务器 ＝ “HTTPS:wgzeyu.github.io[加密,GitHubIO]”
    .如果真 (访问结果 ≠ “”)  ' 2其次使用带HTTPS的GitHub读取更新
        跳出循环 ()
    .如果真结束

.计次循环尾 ()
.如果真 (访问结果 ＝ “”)
    输出文本 (“检测更新失败”, 真, 真)
    返回 ()
.如果真结束
调试输出 (访问结果)
读取版本 ＝ 到整数 (文本_取出中间文本 (访问结果, “<ver>”, “</ver>”))
调试输出 (“本地版本号：” ＋ 到文本 (当前版本) ＋ “ 在线版本号：” ＋ 到文本 (读取版本) ＋ “ 使用服务器：” ＋ 使用服务器)
.如果真 (读取版本 ＞ 当前版本)
    转换为IP地址 (网页_取域名 (文本_取出中间文本 (访问结果, “<url>”, “</url>”)))  ' CloudFlare的DNS太慢了，先解析一次缓存后大概能加快500ms网页打开速度
    更新内容 ＝ “SCFLGT v” ＋ 到文本 (当前版本 ÷ 10) ＋ “→v” ＋ 到文本 (读取版本 ÷ 10) ＋ “ 更新记录：” ＋ #换行符
    .计次循环首 (读取版本 － 当前版本, 已次数)
        .如果真 (文本_取出中间文本 (访问结果, “<” ＋ 到文本 (读取版本 － 已次数 ＋ 1) ＋ “>”, “</” ＋ 到文本 (读取版本 － 已次数 ＋ 1) ＋ “>”) ≠ “”)
            更新内容 ＝ 更新内容 ＋ 文本_取出中间文本 (访问结果, “<” ＋ 到文本 (读取版本 － 已次数 ＋ 1) ＋ “>”, “</” ＋ 到文本 (读取版本 － 已次数 ＋ 1) ＋ “>”) ＋ #换行符
        .如果真结束

    .计次循环尾 ()
    输出文本 (#换行符 ＋ 文本_取出中间文本 (访问结果, “<txt>”, “</txt>”) ＋ #换行符 ＋ “下载地址：” ＋ 文本_取出中间文本 (访问结果, “<url>”, “</url>”))
    .如果 (读取版本 － 当前版本 ≥ 5)
        信息框 (转换编码 (文本_取出中间文本 (访问结果, “<txt>”, “</txt>”) ＋ #换行符 ＋ #换行符 ＋ 更新内容 ＋ #换行符 ＋ “您的版本过低，请更新！”), #警告图标 ＋ #确认钮, 转换编码 (“当前软件版本过低”), _启动窗口)
        网页_打开指定网址 (文本_取出中间文本 (访问结果, “<url>”, “</url>”))
    .否则
        .如果真 (信息框 (转换编码 (文本_取出中间文本 (访问结果, “<txt>”, “</txt>”) ＋ #换行符 ＋ #换行符 ＋ 更新内容 ＋ #换行符 ＋ “是否更新？”), 36, 转换编码 (“发现新版本”), _启动窗口) ＝ 5)
            网页_打开指定网址 (文本_取出中间文本 (访问结果, “<url>”, “</url>”))
        .如果真结束

    .如果结束

.如果真结束


.子程序 _按钮4_被单击

_超文本浏览框1_已就绪 ()
_超文本浏览框1_载入完毕 ()
.版本 2

.程序集 窗口程序集_窗口1

.子程序 _按钮1_被单击

游戏ID ＝ 编辑框1.内容
销毁 ()

.子程序 _窗口1_创建完毕

.如果真 (是否繁体系统)
    按钮1.标题 ＝ 转换编码 (按钮1.标题)
    标签1.标题 ＝ 转换编码 (标签1.标题)
.如果真结束

.版本 2

.全局变量 是否繁体系统, 逻辑型
.全局变量 游戏ID, 文本型
.全局变量 全部好友列表, sc好友, , "0"
.全局变量 全部好友数量, 整数型
.全局变量 当前好友位置, 整数型
.全局变量 上次浏览时间, 长整数型
.全局变量 是否停止, 逻辑型
.全局变量 是否限速, 逻辑型
.版本 2

.数据类型 sc好友
    .成员 游戏ID, 文本型
    .成员 最后登录, 长整数型
    .成员 最后登录日期, 文本型
.版本 2

.DLL命令 GetSystemDefaultLCID, 整数型, "kernel32", "GetSystemDefaultLCID"
